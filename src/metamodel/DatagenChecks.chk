import datagen;
import rdbmdl;

extension org::openarchitectureware::util::stdlib::naming;
extension template::DatagenExtensions; 

context DBAccessModel ERROR "codice prodotto mancante" :
	!isNullOrEmpty(codProdotto);
	
context DBAccessModel ERROR "codice componente mancante" :
	!isNullOrEmpty(codComponente);

context DataAccessObject ERROR "Occorre specificare il nome del DAO:" :
	!isNullOrEmpty(name);

context DataAccessObject if (pkGenerator != null && pkGenerator.metaType == SequencePKGenerator) 
ERROR "Il SequencePKGenerator è utilizzabile solo con PK di 1 sola colonna":
	mainTable.primaryKey.includedColumns.size == 1;
	
/// verifiche sui finder
context DataAccessObject WARNING "il dao:"+this.name+" non definisce i finder base (findByPK e findAll)" :
	finders != null && finders.finders.select(f| (f.metaType==FindAll || f.metaType==FindByPK)).size==2;

context DataAccessObject WARNING "il dao: "+this.name+" non definisce nessun inserter" :
	inserter != null;

context DataAccessObject WARNING "il dao: "+this.name+" non definisce nessun updater di base (UpdateRow)" :
	updaters != null && updaters.updaters.select(f| (f.metaType==UpdateRow)).size==1;

context DataAccessObject ERROR "I nomi dei metodi contenuti nel dao : "+this.name+" devono essere univoci" :
	areAllDaoMethodsNamesUnivocal(inserter,finders,updaters,deleters);
	
context Finder if (metaType != FindByPK && metaType != FindAll && metaType != QBEFinder) ERROR "E' necessario specificare il nome dei finder (esclusi il findByPK, findAll, QBEFinder)"+((DataAccessObject)(this.eContainer.eContainer)).name:
	!isNullOrEmpty(name);

//verifica sulla presenza di una PK in tabella se e' stato definito un finder di tipo findByPK	
context DataAccessObject if(this.finders!= null && this.finders.finders.size>0 && this.finders.finders.select(f|f.metaType == FindByPK).size >0) ERROR "E' stato dichiarato un finder di tipo findByPK per il dao: "+this.loc()+" , ma la tabella di riferimento non possiede chiavi primarie."	:
	this.mainTable.primaryKey != null;
	
//verifica sulla presenza di una PK in tabella se e' stato definito un u di tipo findByPK	
context DataAccessObject if(this.deleters!= null && this.deleters.deleters.size>0 && this.deleters.deleters.select(d|d.metaType == DeleteByPK).size >0) ERROR "E' stato dichiarato un deleter di tipo deleteByPK per il dao: "+this.loc()+" , ma la tabella di riferimento non possiede chiavi primarie."	:
	this.mainTable.primaryKey != null;	
	
context Updater if (metaType != UpdateRow) ERROR "E' necessario specificare il nome degli updater (escluso l'UpdateRow)"+((DataAccessObject)(this.eContainer.eContainer)).name:
	!isNullOrEmpty(name);

context DataAccessObject if(this.updaters!= null && this.updaters.updaters.size >0  && this.updaters.updaters.select(u|u.metaType == UpdateRow || u.metaType == UpdateColumns).size > 0 ) ERROR "E' stato definito almeno un updater di base (row/column), senza che sia stata definita una PK sulla tabella definita dal dao: " +this.loc():
     this.mainTable.primaryKey!= null ;


context CustomFinder ERROR "E' necessario specificare la classe del parametro di filtro (full qualified name, oppure "+
" '$dto' per utilizzare la classe del DTO associata al DAO, o '$dto[]' per utilizzare una"+
"lista di oggetti DTO):"+loc() :
	this.filterClassFQN!=null && this.filterClassFQN != "";

context CustomFinder if (customSelect) ERROR "La clausola distinct è incompatibile con l'attributo 'customSelect'"+loc() :
	this.distinct.size==0;

context Deleter if (metaType != DeleteByPK) ERROR "E' necessario specificare il nome dei deleter (escluso il DeleteByPK)"+((DataAccessObject)(this.eContainer.eContainer)).name:
	!isNullOrEmpty(name);
	
context MultiResFinder if (!isNullOrEmpty(distinct) && orderSpecs!=null && !isNullOrEmpty(orderSpecs.specs)) ERROR "Se la ricerca per distinct è abilitata, tutte le eventuali colonne presenti nella OrderSpecs devono essere presenti anche in distinct. "+((DataAccessObject)(this.eContainer.eContainer)).name:
	areDistinctAndOrderByClauseConsistent(distinct,orderSpecs.specs);

//context LookupResolver ERROR "I lookup-resolver devono specificare la FK risolta:"+loc() :
//	this.foreignKey != null;
	
//context LookupResolver ERROR "I lookup-resolver devono specificare il DAO supplier:"+loc() :
//	this.supplierDAO != null;
	
//check: se ho un lookup resolver deve essere definito almeno un resolver
//context LookupResolvers ERROR "E' stato definito un Lookup resolver, ma non e' stato definito alcun resolver al suo interno!" + loc() :
//	this.resolvers.size >0;	
	

//context LookupResolver if supplierDAO!=null && foreignKey != null && supplierDAO.mainTable!=null
//	ERROR "Il DAO "+supplierDAO.name+", specificato come supplier, è relativo a una "+
//		  "tabella differente da quella specificata nella FK da risolvere ("+foreignKey.name+")" :
//		 supplierDAO.mainTable == ((Table)(foreignKey.referredUC.eContainer));

context LookupResolver ERROR "Lookup Resolver non supportati in questa release":
	false;
		 
//check: se ho un lookup resolver devono esistere tutte le findByPk associate alle tabelle referenziate.
context LookupResolver if supplierDAO!=null ERROR "E' stato definito un lookup resolver, ma non e' stata definita il finder PK per il dao supplier: "+this.supplierDAO.name:
			this.supplierDAO.finders.finders.typeSelect(FindByPK).size > 0;
			
			
//context DaoPackage ERROR "Cycle loop in resolvers!!!":
//		areResolversInLoop(this);
		
//limitazione temporanea: se viene dichiarato un resolver in cui la FK contiene almeno una includedColumn che e' PK e 
//context DataAccessObject if(this.lookupResolvers!=null && this.lookupResolvers.resolvers.size>0) ERROR "Errore: " +loc():
//	   this.lookupResolvers.resolvers.select(r|r.foreignKey.includedColumns.select(tc|tc.isPrimaryKey).size>0).size==0;

//limitazione temporanea 
//context LookupResolver  ERROR "Non e' possibile definire un resolver in cui e' presente una foreign key (<" +this.foreignKey.name + ">) gia' inclusa come primary key in un altro resolver, verificare i resolver definiti nel dao:"+ ((DataAccessObject)(this.eContainer.eContainer)).name :
//	   this.foreignKey.includedColumns.select(tc|tc.isPrimaryKey).size == 0;
	   		 	
//// limitazioni temporanee
context QBEFinder ERROR "Il QBEFinder non può funzionare in modalità DISTINCT" :
this.distinct.size==0;

//context QBEFinder ERROR "Il QBEFinder non supporta la clausola ORDER BY" :
//this.orderSpecs==null;

// check sulla cache: cache impostata (ALMOST_STABLE_DATA) ma TTL minore zero
context DataAccessObject if(this.cacheability==CacheabilityTypes::ALMOST_STABLE_DATA && this.cacheTTLminutes<0) ERROR "E' stato impostato un TTL minore di zero per il dao: "+loc()+ " , dato non consistente":
false;

// check sulla cache: cache non impostata (HIGHLY_VARIABLE_DATA) ma TTL diverso da zero
context DataAccessObject if(this.cacheability==CacheabilityTypes::HIGHLY_VARIABLE_DATA) WARNING "E' stato impostato un TTL diverso da zero per il dao: "+loc()+ " , ma la cache non e' abilitata (HIGHLY_VARIABLE_DATA)":
!(this.cacheTTLminutes != 0);

// check sulla cache: cache impostata ma TTL zero: usare il default di 24 ore!!!
context DataAccessObject if(this.cacheability==CacheabilityTypes::ALMOST_STABLE_DATA)  WARNING "E' stato impostata la cache per il dao " +loc()+" , con TTL pari zero, pertanto si utilizza il default di 24hr":
!(this.cacheTTLminutes == 0);





